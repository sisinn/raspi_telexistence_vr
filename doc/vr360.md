# 全天球カメラとVRゴーグルで、テレイグジスタンス体験(8)

[前ページ](./openframeworks.md) / [top](./top.md) / [次ページ](./3dof.md)  

## 8. 全天球映像からVR表示生成  

床井研究室の情報を元に、シェーダプログラムを作成する。  
[魚眼レンズ画像の平面展開のサンプルプログラム](http://marina.sys.wakayama-u.ac.jp/~tokoi/?date=20161203)  

※ラズパイは、OpenGL ES 2.0なので、シェーディング言語はGLSL ES 1.0  
　上記情報は、GLSL 1.5なので、置き換え必要  

ソースコード全体は、GitHub参照  
ポイントのみ抜粋して説明を記載する。  

OpenGL自体の概要は、以下のサイトを参照させていただいた  
* [OpenGL が世界を描画する仕組み](http://tkengo.github.io/blog/2014/12/27/opengl-es-2-2d-knowledge-1/)  
* [GLSL(shader言語)](http://www.gamvaro.com/kswiki/index.php?3D-Graphics/GLSL%28shader%E8%A8%80%E8%AA%9E%29)  

### ■openFrameworks側  
---

モニターの表示領域は、横：`ofGetWidth()`、縦：`ofGetHeight()`  
この領域に対し、等間隔に点を打つ：`Mesh[i].draw()`  
点の数だけ、バーテックスシェーダ処理が呼び出される。  
細かくすればするほど、全天球→モニターへの変換が滑らかになるが、ハードの処理性能が必要になる。  

Meshの理解は、は、以下のサイトを参照させていただいた  
* [openFrameworksの3D図形の色々な描画方法](https://ayumu-nagamatsu.com/archives/579/)  
* [ofMeshの基礎](https://qiita.com/YoshimasaSakuragi/items/2734ff24e2f6a2e5aafe)  

シェーダ内で演算するために、パラメータをいくつか渡す。  

|変数|型|意味|
|:---|:---|:---|
|`tex0`|テクスチャ|動画データの出力|
|`resolution`|2次元ベクトル（横、縦）|動画データの解像度|
|`screen`|2次元ベクトル（横、縦）|モニターの解像度|
|`focal`|浮動小数点|視点から描画領域＝モニターまでの距離|
|`invTmax`|浮動小数点|レンズ画角＝天頂角 (θmax) の逆数　１／（２πｒ＊θ／３６０）|
|`smooth`|浮動小数点|全天球画像の左右の混合率|
|`radius_b`|2次元ベクトル（横方向の半径、縦方向の半径）|全天球画像の左側円の半径指定|
|`center_b`|2次元ベクトル（横、縦）|全天球画像の左側円の中心指定|
|`radius_f`|2次元ベクトル（横方向の半径、縦方向の半径）|全天球画像の右側円の半径指定|
|`center_f`|2次元ベクトル（横、縦）|全天球画像の右側円の中心指定|
|`mo`|4x4回転行列|頭の向き|

`ofApp::draw()`内の処理で、シェーダを使用する。  
``` cpp
shader.begin();
    shader.setUniformTexture("tex0", omxPlayer.getTextureReference(), omxPlayer.getTextureID());
    shader.setUniform2f("resolution", movie_plane_width, movie_plane_height);
    shader.setUniform2f("screen", ofGetWidth(), ofGetHeight());

    shader.setUniform1f("focal", focal);
    shader.setUniform1f("invTmax", invTmax);
    shader.setUniform1f("smooth", smooth);
    shader.setUniform2fv("radius_b", radius_b);
    shader.setUniform2fv("center_b", center_b);
    shader.setUniform2fv("radius_f", radius_f);
    shader.setUniform2fv("center_f", center_f);
    shader.setUniformMatrix4f("mo", modelMatrix);

    for(int i = 0; i < PRIMITIVE_COUNT; i++){
        Mesh[i].draw();
    }
shader.end();
```

本プログラムでは、OpenGLの3次元空間として、上記Meshだけを配置する。  
視点とMeshだけになり、Mesh領域全体をモニターに移すので、配置は気にしない。  
動画データは、Mesh上にテクスチャとして張り付けるので、OpenGLの3次元空間の配置とは関係ない。  

### ■バーテックスシェーダ側  
---

#### モニターに表示する領域の指定  
`position`及び`modelViewProjectionMatrix`は、openFrameworksでデフォルトで提供されるパラメータ（宣言は必要）  
* `position`は、Meshオブジェクト自体の各点の座標  
* `modelViewProjectionMatrix`は、「Object Space -> World Space -> Eye Space -> Clip Space」の変換行列  
    以下を参照  
    [openFrameworksでシェーダを扱う](https://mactkg.hateblo.jp/entry/2014/02/05/013421)  

Meshオブジェクト全体をそのままモニターに出力する。  
``` c
gl_Position = modelViewProjectionMatrix * position;
```

※`gl_Position`は、OpenGL自体の変数  

#### テクスチャ座標の計算  

モニター座標は、縦横それぞれ解像度と同じ粒度であるが、テクスチャ自体は正規化されて［0.0,1.0］の範囲で座標を示す点に注意  
``` c
// 視線ベクトル
// 視点から、スクリーン上（スクリーン中心が原点）に対する位置pに向かう視線は、
// 焦点距離 focal を Z 座標に用いて (p, -focal) となる。
// これを回転したあと正規化して、その方向の視線単位ベクトルを得る
vec2 p = vec2((position.x - (screen.x / 2.0)) / screen.y , (position.y - (screen.y / 2.0)) / screen.y);
vec4 vr_vector = mo * normalize(vec4(p, -focal, 1.0));

// 視線ベクトルの相対的な仰角
//   1 - acos(vector.z) * 2 / π → [-1, 1]
float angle = 1.0 - (acos(vr_vector.z) * invTmax);

// 視線ベクトルの xy 上での方向ベクトル
vec2 orientation = vr_vector.xy;

// テクスチャ座標
texcoord_b = (1.0 - angle) * orientation * radius_b + center_b;
texcoord_f = (1.0 + angle) * orientation * radius_f + center_f;
// 前後のテクスチャの混合比
blend = smoothstep(-1.0 * smooth, smooth, angle);
```

パラメータ`texcoord_b`、`texcoord_f`、`blend`を、フラグメントシェーダに渡す。

### ■フラグメントシェーダ側  
---

フラグメントシェーダは、画素ごとに呼び出される。  
※バーテックスシェーダと、フラグメントシェーダは、処理粒度が異なる。  
　バーテックスシェーダから渡されたパラメータ`texcoord_b`、`texcoord_f`、`blend`は、  
　OpenGL内部で適切に補完されてから、フラグメントシェーダに渡される。  

``` c
// 前後のテクスチャの色をサンプリングする
vec4 color_b = texture2D(tex0, texcoord_b);
vec4 color_f = texture2D(tex0, texcoord_f);

// サンプリングした色をブレンドしてフラグメントの色を求める
gl_FragColor = mix(color_f, color_b, blend);
```

※gl_FragColorは、OpenGL自体の変数  


[前ページ](./openframeworks.md) / [top](./top.md) / [次ページ](./3dof.md)  

